apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0
def versionBuild = 0

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "ru.annin.autorespect"
        minSdkVersion 15
        targetSdkVersion 25

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")

        resConfigs "en", "ru"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    productFlavors {
        dev {
            buildConfigField "String", "API_BASE_URL", '"http://192.168.1.1"'
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "D"
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    applicationVariants.all { variant ->
        File outDir = file("${rootDir}/out/${variant.buildType.name}")
        if (!outDir.exists()) { outDir.mkdirs() }
        variant.assemble.doLast {
            variant.outputs.each { output ->
                File apkFile = output.outputFile
                copy {
                    from apkFile
                    into outDir
                    rename { fileName -> "AutoRespect_v${variant.versionName}.apk" }
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Library
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // MVP
    compile 'com.arello-mobile:moxy:1.3.3'
    compile 'com.arello-mobile:moxy-app-compat:1.3.3'
    kapt 'com.arello-mobile:moxy-compiler:1.3.3'

    // Rest Api
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.1.0'

    // Rx
    compile 'io.reactivex:rxjava:1.2.4'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxkotlin:0.60.0'

    // Test
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

kapt {
    generateStubs = true
}